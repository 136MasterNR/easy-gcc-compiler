@CHCP 65001 >NUL
@VERIFY OFF
@TITLE C Language : GCC-COMPILER v0.1.2
@ECHO OFF


REM CONFIG
:: Language
SET "LANGUAGE=GR" &:: EN or GR

:: File name
SET "FNAME=main"

:: Which file to compile
SET "TARGET=%CD%\%FNAME%.c"

:: Where to compile
SET "OUT=%CD%\%FNAME%.exe"

:: Path for the GCC compiler.
SET "COMPILER_PATH=C:\gcc"

:: Temp rememberme file.
SET "TMP_RMB=%TMP%\gcc_rememberme.cmd"


:: Overwrite target there is a file has opened before.
IF EXIST "%TMP_RMB%" (
	CALL "%TMP_RMB%"
)

:: Overwrite target if arguments are given.
SET ARG1=%1
SET ARG_OUT=%~dpn1
IF DEFINED ARG1 (
	SET TARGET=%ARG1:"=%
	SET "OUT=%ARG_OUT%.exe"
	(
		ECHO.SET "TARGET=%ARG1:"=%"
		ECHO.SET "OUT=%ARG_OUT%.exe"
	) > "%TMP_RMB%" || (
		IF %LANGUAGE%==EN ECHO.[1;33mWARNING[0m: [1mUnable to remember last opened file.
		IF %LANGUAGE%==GR ECHO.[1;33mÎ Î¡ÎŸÎ•Î™Î”ÎŸÎ ÎŸÎ™Î—Î£Î—[0m: [1mÎ‘Î´Ï…Î½Î±Î¼Î¯Î± Î±Ï€Î¿Î¼Î½Î·Î¼ÏŒÎ½ÎµÏ…ÏƒÎ·Ï‚ Ï„Î¿Ï… Ï„ÎµÎ»ÎµÏ…Ï„Î±Î¯Î¿Ï… Î±Î½Î¿Î¹Î³Î¼Î­Î½Î¿Ï… Î±ÏÏ‡ÎµÎ¯Î¿Ï….
	)
)


:: Detect whether GCC is installed.
IF EXIST "%COMPILER_PATH%\bin\gcc.exe" (
	:: If it is, run the compiler.
	GOTO COMPILER
) ELSE (
	:: If it isn't, install it automatically.
	GOTO INSTALLER
)

EXIT

:INSTALLER
IF %LANGUAGE%==EN (
	ECHO.The compiler (GCC) is not installed on your computer.
	CHOICE /C YN /N /M "Do you want to install GCC essentials (Y/N)?"
)
IF %LANGUAGE%==GR (
	ECHO.ÎŸ Î¼ÎµÏ„Î±Î³Î»Ï‰Ï„Ï„Î¹ÏƒÏ„Î®Ï‚ (GCC) Î´ÎµÎ½ ÎµÎ¯Î½Î±Î¹ ÎµÎ³ÎºÎ±Ï„ÎµÏƒÏ„Î·Î¼Î­Î½Î¿Ï‚ ÏƒÏ„Î¿Î½ Ï…Ï€Î¿Î»Î¿Î³Î¹ÏƒÏ„Î® ÏƒÎ±Ï‚.
	CHOICE /C YN /N /M "Î˜Î­Î»ÎµÏ„Îµ Î½Î± ÎµÎ³ÎºÎ±Ï„Î±ÏƒÏ„Î®ÏƒÎµÏ„Îµ Ï„Î± Î±Ï€Î±ÏÎ±Î¯Ï„Î·Ï„Î± Ï€Î±ÎºÎ­Ï„Î± Ï„Î¿Ï… GCC ÏƒÏ„Î¿Î½ Ï…Ï€Î¿Î»Î¿Î³Î¹ÏƒÏ„Î® ÏƒÎ±Ï‚; (Y/N)"
)

IF ERRORLEVEL 2 (
	:: If N is pressed, exit.
	EXIT 1
)

IF %LANGUAGE%==EN (
	TITLE Downloading ...
	ECHO.Downloading ...
)
IF %LANGUAGE%==GR (
	TITLE Î“Î¯Î½ÎµÏ„Î±Î¹ Î»Î®ÏˆÎ· ...
	ECHO.Î“Î¯Î½ÎµÏ„Î±Î¹ Î»Î®ÏˆÎ· ...
)
IF EXIST ".\gcc.zip" DEL /F /Q ".\gcc.zip"
CURL -S "https://img.comradeturtle.dev/assets/gcc.zip" --SSL-NO-REVOKE >".\gcc.zip"

ECHO.
IF %LANGUAGE%==EN (
	TITLE Installing ...
	ECHO.Installing ...
)
IF %LANGUAGE%==GR (
	TITLE Î•Î³ÎºÎ±Ï„Î¬ÏƒÏ„Î±ÏƒÎ· ...
	ECHO.Î•Î³ÎºÎ±Ï„Î¬ÏƒÏ„Î±ÏƒÎ· ...
)
SET VBS=".\gccInstaller.VBS"
IF EXIST "%VBS%" DEL /F /Q "%VBS%"
>%VBS% ECHO Set fso = CreateObject("Scripting.FileSystemObject")
>>%VBS% ECHO If NOT fso.FolderExists("%COMPILER_PATH%") Then
>>%VBS% ECHO fso.CreateFolder("%COMPILER_PATH%")
>>%VBS% ECHO End If
>>%VBS% ECHO set objShell = CreateObject("Shell.Application")
>>%VBS% ECHO set FilesInZip=objShell.NameSpace("%CD%\gcc.zip").items
>>%VBS% ECHO objShell.NameSpace("%COMPILER_PATH%").CopyHere(FilesInZip)
>>%VBS% ECHO Set fso = Nothing
>>%VBS% ECHO Set objShell = Nothing
CSCRIPT //NOLOGO "%VBS%"

IF %LANGUAGE%==EN ECHO.Cleaning up ...
IF %LANGUAGE%==GR ECHO.ÎšÎ±Î¸Î±ÏÎ¹ÏƒÎ¼ÏŒÏ‚ ...
IF EXIST "%VBS%" DEL /F /Q "%VBS%"
IF EXIST "%CD%\gcc.zip" DEL /F /Q "%CD%\gcc.zip"

IF EXIST "%COMPILER_PATH%\bin\gcc.exe" (
	IF %LANGUAGE%==EN ECHO.The installation has finished successfuly.
	IF %LANGUAGE%==GR ECHO.Î— ÎµÎ³ÎºÎ±Ï„Î¬ÏƒÏ„Î±ÏƒÎ· Î¿Î»Î¿ÎºÎ»Î·ÏÏŽÎ¸Î·ÎºÎµ Î¼Îµ ÎµÏ€Î¹Ï„Ï…Ï‡Î¯Î±.
) ELSE (
	IF %LANGUAGE%==EN ECHO.The installation wasn't successful.
	IF %LANGUAGE%==GR ECHO.Î— ÎµÎ³ÎºÎ±Ï„Î¬ÏƒÏ„Î±ÏƒÎ· Î´ÎµÎ½ Î®Ï„Î±Î½ ÎµÏ€Î¹Ï„Ï…Ï‡Î®Ï‚.
)
PAUSE>NUL
EXIT

:COMPILER
IF NOT EXIST "%TARGET%" (
	IF %LANGUAGE%==EN ECHO.[1;31mFATAL[0m: [1mCompilation couldn't start, the requested file doesn't exist.[0m
	IF %LANGUAGE%==GR ECHO.[1;31mÎ£Î¦Î‘Î›ÎœÎ‘[0m: [1mÎ— ÏƒÏ…Î½Î±ÏÎ¼Î¿Î»ÏŒÎ³Î·ÏƒÎ· Î´ÎµÎ½ Î¼Ï€ÏŒÏÎµÏƒÎµ Î½Î± Î¾ÎµÎºÎ¹Î½Î®ÏƒÎµÎ¹, Ï„Î¿ Î±ÏÏ‡ÎµÎ¯Î¿ Ï€Î¿Ï… ÎµÎ¹ÏƒÎ¬Î³Î±Ï„Îµ Î´ÎµÎ½ Ï…Ï€Î±ÏÏ‡ÎµÎ¹.[0m
	PAUSE>NUL
	EXIT 1
)

DEL /Q "%OUT%" 2>NUL

IF %LANGUAGE%==EN TITLE Compiling ...
IF %LANGUAGE%==GR TITLE Î£Ï…Î½Î±ÏÎ¼Î¿Î»ÏŒÎ³Î·ÏƒÎ· ...

SET OUTPUT=FALSE

:: The compiler can be configured from here. To run C++, change the -std switch to "c++17".
FOR /F %%I IN ('START /B /W "" "%COMPILER_PATH%\bin\gcc.exe" -Wall -Wextra -g3 -O0 -std^=c11 "%TARGET%" -o "%OUT%"') DO (
    SET "OUTPUT=TRUE"
)

IF EXIST "%OUT%" (
	START CMD /C TITLE  ^& "%OUT%" ^& PAUSE^>NUL
	IF %OUTPUT%==TRUE PAUSE>NUL
) ELSE (
	ECHO.
	IF %LANGUAGE%==EN ECHO.[1;31mFATAL[0m: [1mCompilation process failed, see above for hints.[0m
	IF %LANGUAGE%==GR ECHO.[1;31mÎ£Î¦Î‘Î›ÎœÎ‘[0m: [1mÎ‘Ï€Î¿Ï„Ï…Ï‡Î¯Î± ÏƒÏ…Î½Î±ÏÎ¼Î¿Î»ÏŒÎ³Î·ÏƒÎ·Ï‚, Î´ÎµÎ¯Ï„Îµ Ï€Î±ÏÎ±Ï€Î¬Î½Ï‰ Î³Î¹Î± Ï€ÎµÏÎ¹ÏƒÏƒÏŒÏ„ÎµÏÎµÏ‚ Ï€Î»Î·ÏÎ¿Ï†Î¿ÏÎ¯ÎµÏ‚.[0m
	PAUSE>NUL
	EXIT 1
)

EXIT
